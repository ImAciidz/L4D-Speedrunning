////////////////////////////////
// MAIN CAMPAIGNS VOTE CYCLER //
////////////////////////////////



///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// To rearrange the sequence, ONLY update BOTH instances of the campaign name seen in the first 5 entries below. //
// To vote for the campaign currently selected in sequence, the command is "CampaignVote".                       //
// To reset the sequence back to the beginning, the command is "CampaignReset".                                  //
// To advance the sequence by one campaign without voting, the command is "CampaignNext".                        //
// To revert the sequence by one campaign without voting, the command is "CampaignPrev".                         //
// See the bottom of this script for examples on how to bind these properly to a key.                            //
// Use "exec MainCampaignsCycler" in the console to activate this script.                                        //
// Chat has a short cooldown so if campaigns are rotated too quickly it may not print the text, but it did work! //
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////



// If confused on how to change your order, check out https://github.com/ImAciidz/L4D-Speedrunning/wiki/Campaign-Cyclers or ask in the speedrunning Discord
// Basically just change both instances of a number to another (change both instances of 01 to 03, and change both instances of 03 to 01, for example)

// THIS IS WHAT DEFINES THE CAMPAIGN ORDER
alias Campaign01 SwampFever;	alias Campaign01Say "say Swamp Fever selected"
alias Campaign02 DeadCenter;	alias Campaign02Say "say Dead Center selected"
alias Campaign03 DarkCarnival;	alias Campaign03Say "say Dark Carnival selected"
alias Campaign04 HardRain;		alias Campaign04Say "say Hard Rain selected"
alias Campaign05 TheParish;		alias Campaign05Say "say The Parish selected"

// DO NOT TOUCH THESE UNLESS YOU KNOW WHAT YOU'RE DOING
alias Campaign01Next "alias CampaignVote Campaign02; Campaign02Say; alias CampaignNext Campaign02Next; alias CampaignPrev Campaign02Prev"
alias Campaign02Next "alias CampaignVote Campaign03; Campaign03Say; alias CampaignNext Campaign03Next; alias CampaignPrev Campaign03Prev"
alias Campaign03Next "alias CampaignVote Campaign04; Campaign04Say; alias CampaignNext Campaign04Next; alias CampaignPrev Campaign04Prev"
alias Campaign04Next "alias CampaignVote Campaign05; Campaign05Say; alias CampaignNext Campaign05Next; alias CampaignPrev Campaign05Prev"
alias Campaign05Next "alias CampaignVote Campaign01; Campaign01Say; alias CampaignNext Campaign01Next; alias CampaignPrev Campaign01Prev"

// DO NOT TOUCH THESE UNLESS YOU KNOW WHAT YOU'RE DOING
alias Campaign01Prev "alias CampaignVote Campaign05; Campaign05Say; alias CampaignNext Campaign05Next; alias CampaignPrev Campaign05Prev"
alias Campaign02Prev "alias CampaignVote Campaign01; Campaign01Say; alias CampaignNext Campaign01Next; alias CampaignPrev Campaign01Prev"
alias Campaign03Prev "alias CampaignVote Campaign02; Campaign02Say; alias CampaignNext Campaign02Next; alias CampaignPrev Campaign02Prev"
alias Campaign04Prev "alias CampaignVote Campaign03; Campaign03Say; alias CampaignNext Campaign03Next; alias CampaignPrev Campaign03Prev"
alias Campaign05Prev "alias CampaignVote Campaign04; Campaign04Say; alias CampaignNext Campaign04Next; alias CampaignPrev Campaign04Prev"

// Defines the campaign votes (CAN REPLACE VOTES WITH MAP COMMANDS DEPENDING ON THE SITUATION)
alias DeadCenter "callvote changemission L4D2C1"
alias DarkCarnival "callvote changemission L4D2C2"
alias SwampFever "callvote changemission L4D2C3"
alias HardRain "callvote changemission L4D2C4"
alias TheParish "callvote changemission L4D2C5"

// Resetter
alias CampaignReset "Campaign01; Campaign01Next"
Campaign01Next // Initialize

// REMOVE "//" FROM THE BEGINNING OF THE BINDS YOU WANT TO USE, OTHERWISE THE GAME WILL NOT READ IT
// REPLACE THE "YOURKEYHERE" WITH THE KEY YOU WANT. NUMPAD BINDS ARE PREFIXED WITH "KP_". ASK IN DISCORD IF YOU NEED HELP 

//bind YOURKEYHERE "CampaignVote; CampaignNext; vote yes" 	// Votes the campaign in the sequence, rotates to the next one, and votes yes (useful for co-op)
//bind YOURKEYHERE "CampaignReset; vote yes" 		 		// Votes for the first campaign, resets the sequence, and votes yes (useful for co-op)
//bind YOURKEYHERE CampaignNext 					 		// Advances the sequence forward to prime it for voting
//bind YOURKEYHERE CampaignPrev 							// Reverts the sequence backward to prime it for voting